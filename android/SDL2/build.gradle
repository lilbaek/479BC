apply plugin: 'com.android.library'

def findLibrary(String prefix) {
    def extPath = project.file("../../ext/SDL2")
    def files = extPath.list({f, name -> name.startsWith("$prefix-")})
    if (files.length == 0) {
        throw new IllegalStateException("$prefix source not found, please extract the source to a subdirectory named '$prefix-2.0.*' in: " + extPath.absolutePath)
    } else if (files.length > 1) {
        throw new IllegalStateException("Multiple $prefix source folders found, please make sure there is only one folder named '$prefix-2.0.*' in: " + extPath.absolutePath)
    }
    return new File(extPath, files[0])
}

task findSDL2 {
    def path = findLibrary("SDL2")
    ext.sourcePath = path
    ext.includePath = new File(path, "include")
}

task findSDL2Mixer {
    def path = findLibrary("SDL2_mixer")
    ext.sourcePath = path
    // SDL2_mixer doesn't have a dedicated directory for includes. If we use the root dir as
    // include_path, the whole source dir is packaged in the .aar, which is not what we want.
    // Workaround: copy the headers to our own directory for includes.
    def includeTarget = layout.buildDirectory.dir("sdl2_mixer_includes")
    copy {
        from path
        include "*.h"
        into includeTarget
    }
    ext.includePath = includeTarget.get().asFile
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"
    ndkVersion "23.1.7779620"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            ndkBuild {
                cFlags '-DMUSIC_WAV'
                arguments 'SUPPORT_FLAC=false',
                        'SUPPORT_OGG=false',
                        'SUPPORT_MOD_MODPLUG=false',
                        'SUPPORT_MID_TIMIDITY=false'
            }
        }
        sourceSets {
            main.java.srcDirs += "${findSDL2.sourcePath}/android-project/app/src/main/java"
        }
    }
    buildFeatures {
        prefabPublishing true
    }
    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path '../../ext/SDL2/Android.mk'
        }
    }
    prefab {
        SDL2 {
            headers "${findSDL2.includePath}"
        }
        SDL2_mixer {
            headers "${findSDL2Mixer.includePath}"
        }
    }
    namespace 'com.github.bvschaik.julius.sdl2'
}

repositories {
    google()
    mavenCentral()
}

// Workaround for issue: https://issuetracker.google.com/issues/222811915
// Create prefab dir to prevent failing Gradle Sync in Android Studio.
// Sync will throw a warning (instead of error) but the project will build normally.
mkdir "$buildDir/intermediates/prefab_package_configuration/debug/prefab"
